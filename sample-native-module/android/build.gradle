// `build.gradle` file is like "`package.json`" for Android packages

buildscript {
  // Helper lambda function for safely getting properties from the application root project
  ext.safeExtGet = {prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  def kotlin_version = safeExtGet('kotlinVersion', '1.6.10') // Kotlin setup - declaring Kotlin version

  repositories {
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath("com.android.tools.build:gradle:7.2.1")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version") // Kotlin setup - adding Kotlin Gradle plugin
  }
}

// Detect if new arch is enabled
def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

// Apply plugins to make it an android library
apply plugin: 'com.android.library'

// Kotlin setup - applying Kotlin Gradle plugin
apply plugin: 'kotlin-android'

// react-native-gradle-plugin if new arch is enabled
if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

// Minimal configuration to make library usable on Android API 21-33
android {
  compileSdkVersion safeExtGet('compileSdkVersion', 33)
  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 33)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }

  // Add (or remove) additional paths to the library's source code
  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        // For new arch add newarch sourceSet & react-native-codegen's generated java classes and interfaces
        java.srcDirs += ['src/newarch/java', "${project.buildDir}/generated/source/codegen/java"]
      } else {
        // For old arch add oldarch sourceSet
        java.srcDirs += ['src/oldarch/java']
      }
    }
  }
}

// Declare android package repositories (places from where the dependencies can be fetched)
repositories {
  maven {
    url "$projectDir/../node_modules/react-native/android"
  }
  mavenCentral()
  google()
}

// Use some helper methods
apply from: "$projectDir/react-native-helpers.gradle"

// Declare library's dependencies (similar to entries under "dependencies" field in `package.json`)
dependencies {
  if (project.ext.shouldConsumeReactNativeFromMavenCentral()) {
    implementation "com.facebook.react:react-android" // Set by the React Native Gradle Plugin
  } else {
    implementation 'com.facebook.react:react-native:+' // From node_modules
  }
}
