// `build.gradle` file is like "`package.json`" for Android packages

buildscript {
  // Helper lambda function for safely getting properties from the application root project
  ext.safeExtGet = {prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  // Kotlin setup - declaring Kotlin version
  def kotlin_version = safeExtGet('kotlinVersion', '1.6.10')

  repositories {
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath("com.android.tools.build:gradle:7.2.1")
    // Kotlin setup - adding Kotlin Gradle plugin
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
  }
}

// Detect if new arch is enabled
def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

// Apply plugins to make it an android library
apply plugin: 'com.android.library'

// Kotlin setup - applying Kotlin Gradle plugin
apply plugin: 'kotlin-android'

// react-native-gradle-plugin if new arch is enabled
if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

android {
  // Minimal configuration to make library usable on Android API 21-33
  compileSdkVersion safeExtGet('compileSdkVersion', 33)
  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 33)
  }

  // Add (or remove) additional paths to the library's source code
  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        // For new arch add newarch sourceSet & react-native-codegen's generated java classes and interfaces
        java.srcDirs += ['src/newarch/java', "${project.buildDir}/generated/source/codegen/java"]
      } else {
        // For old arch add oldarch sourceSet
        java.srcDirs += ['src/oldarch/java']
      }
    }
  }
}

// Declare android package repositories (places from where the dependencies can be fetched)
repositories {
  maven {
    url "$projectDir/../node_modules/react-native/android"
  }
  mavenCentral()
  google()
}

// Declare library's dependencies (similar to entries under "dependencies" field in `package.json`)
dependencies {
  implementation 'com.facebook.react:react-native:+'
}
